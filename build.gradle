import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

plugins {
  id 'java'
  id 'java-base'
  id 'java-library'
  id 'maven-publish'
  id("io.papermc.paperweight.userdev") version "1.7.2"
  id("xyz.jpenilla.run-paper") version "2.1.0"
  id('com.github.johnrengelman.shadow') version "8.1.1"
}


group = "de.timesnake"
version = '4.0.0'
var projectId = 52

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://repo.papermc.io/repository/maven-public/"
  }
  maven {
    url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
  }
  maven {
    url "https://git.timesnake.de/api/v4/groups/7/-/packages/maven"
    name "TimeSnake"
    credentials {
      username = timesnakeUser
      password = timesnakePassword
    }
  }
}

dependencies {
  implementation "com.moandjiezana.toml:toml4j:0.7.3-SNAPSHOT"

  implementation "de.timesnake:library-entities:3.+"
  implementation "de.timesnake:library-packets:3.+"

  implementation "de.timesnake:library-network:2.+"
  implementation "de.timesnake:library-commands:2.+"
  implementation "de.timesnake:library-permissions:2.+"
  implementation "de.timesnake:library-basic:2.+"
  implementation "de.timesnake:library-chat:2.+"

  implementation "com.google.code.gson:gson:2.10.1"

  compileOnly "de.timesnake:database-bukkit:3.+"
  compileOnly "de.timesnake:database-api:3.+"

  compileOnly "de.timesnake:channel-bukkit:5.+"
  compileOnly "de.timesnake:channel-api:5.+"

  compileOnly "org.freemarker:freemarker:2.3.31"

  paperweight.paperDevBundle("1.21-R0.1-SNAPSHOT")

  testImplementation platform('org.junit:junit-bom:5.10.0')
  testImplementation 'org.junit.jupiter:junit-jupiter'

  testImplementation "de.timesnake:channel-bukkit:5.+"
  testImplementation "de.timesnake:channel-api:5.+"
}

configurations.configureEach {
  resolutionStrategy.dependencySubstitution {
    if (project.parent != null) {
      substitute module("de.timesnake:database-bukkit") using project(":database:database-bukkit")
      substitute module("de.timesnake:database-api") using project(":database:database-api")

      substitute module("de.timesnake:channel-bukkit") using project(":channel:channel-bukkit")
      substitute module("de.timesnake:channel-api") using project(":channel:channel-api")

      substitute module("de.timesnake:library-packets") using project(":libraries-mc:library-packets")
      substitute module("de.timesnake:library-entities") using project(":libraries-mc:library-entities")

      substitute module("de.timesnake:library-network") using project(":libraries:library-network")
      substitute module("de.timesnake:library-commands") using project(":libraries:library-commands")
      substitute module("de.timesnake:library-permissions") using project(":libraries:library-permissions")
      substitute module("de.timesnake:library-basic") using project(":libraries:library-basic")
      substitute module("de.timesnake:library-chat") using project(":libraries:library-chat")
    }
  }
}



tasks.register('exportAsPlugin', Copy) {
  from "$buildDir/libs/${archivesBaseName}-${project.version}.jar"
  into "${timesnakePluginDir}"

  dependsOn(assemble)
}

publishing {
  repositories {
    maven {
      url "https://git.timesnake.de/api/v4/projects/" + projectId + "/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = "Job-Token"
        value = System.getenv("CI_JOB_TOKEN")
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

tasks {
  compileJava {
    options.encoding = "UTF-8"
    options.release = 21
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
  withSourcesJar()
}

paperweight.reobfArtifactConfiguration = ReobfArtifactConfiguration.getMOJANG_PRODUCTION()

test {
  useJUnitPlatform()
}
