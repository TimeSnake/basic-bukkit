plugins {
  id 'java-library'
  id 'maven-publish'
  id('io.papermc.paperweight.userdev')
  id('xyz.jpenilla.run-paper')
  id('com.github.johnrengelman.shadow')
}

group = project.group
version = '3.1.1'
archivesBaseName = 'basic-bukkit'
var projectId = 52

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = uri 'https://papermc.io/repo/repository/maven-public/'
  }
  maven {
    url = uri 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
  }
}

dependencies {
  implementation project.toml
  implementation project.reflection_remapper

  paperweight.paperDevBundle(project.paperweight_version)

  implementation project(':library-network')
  implementation project(':library-entities')
  implementation project(':library-packets')
  implementation project(':library-extension')
  implementation project(':library-basic')
  implementation project(':library-chat')

  implementation project.gson

  compileOnly project(':database-bukkit')
  compileOnly project(':database-api')

  compileOnly project(':channel-bukkit')
  compileOnly project(':channel-api')

  compileOnly project.freemarker
}

tasks {
  assemble {
    dependsOn(reobfJar)
  }

  compileJava {
    options.encoding = "UTF-8"
    options.release.set(17)
  }

  javadoc {
    options.encoding = "UTF-8"
  }

  processResources {
    filteringCharset = "UTF-8"
    var props = [
        "name"      : project.name,
        "version"   : project.version,
        "apiVersion": "1.20.1"
    ]
    inputs.properties(props)
    filesMatching("plugin.yml") {
      expand(props)
    }
  }

  reobfJar {
    outputJar.set(layout.buildDirectory.file("${timesnakePluginDir}/${project.name}-${project.version}.jar"))
  }
}

publishing {
  repositories {
    maven {
        url "https://git.timesnake.de/api/v4/projects/" + projectId + "/packages/maven"
      credentials {
          username = timesnakeDeployUser
          password = timesnakeDeployKey
      }

    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}